// These procs are the ones that will be called from `index.html`, which is
// generated from `index_template.html`.

package scatter_web

import "base:runtime"
import "core:c"
import "core:mem"

@(private = "file")
web_context: runtime.Context

@(export)
main_start :: proc "c" () {
	context = runtime.default_context()

	// The WASM allocator doesn't seem to work properly in combination with
	// emscripten. There is some kind of conflict with how they manage memory.
	// So this sets up an allocator that uses emscripten's malloc.
	context.allocator = emscripten_allocator()
	runtime.init_global_temporary_allocator(1 * mem.Megabyte)

	context.logger = create_emscripten_logger()

	web_context = context

	init()
}

@(export)
main_update :: proc "c" () -> bool {
	context = web_context
	update()
	return should_run()
}

@(export)
main_end :: proc "c" () {
	context = web_context
	shutdown()
}

@(export)
web_window_size_changed :: proc "c" (w: c.int, h: c.int) {
	context = web_context
	parent_window_size_changed(int(w), int(h))
}
